## <summary>Mozilla and related web browsers.</summary>

########################################
## <summary>
##	Role access for mozilla
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`mozilla_role',`
	gen_require(`
		type mozilla_t, mozilla_exec_t, mozilla_home_t;
		type mozilla_tmp_t, mozilla_tmpfs_t;
	')

	role $1 types mozilla_t;

	domain_auto_trans($2, mozilla_exec_t, mozilla_t)

	# Clean this up.
	allow $2 mozilla_t:process { noatsecure siginh rlimitinh };
	allow mozilla_t $2:fd use;

	allow $2 mozilla_t:fd use;
	allow $2 mozilla_t:shm { associate getattr };
	allow $2 mozilla_t:shm { unix_read unix_write };

	ps_process_pattern($2, mozilla_t)
	allow $2 mozilla_t:process { ptrace signal_perms };

	manage_dirs_pattern($2, mozilla_tmp_t, mozilla_tmp_t)
	manage_files_pattern($2, mozilla_tmp_t, mozilla_tmp_t)
	manage_sock_files_pattern($2, mozilla_tmp_t, mozilla_tmp_t)

	relabel_dirs_pattern($2, mozilla_tmp_t, mozilla_tmp_t)
	relabel_files_pattern($2, mozilla_tmp_t, mozilla_tmp_t)
	relabel_sock_files_pattern($2, mozilla_tmp_t, mozilla_tmp_t)

	manage_fifo_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)
	manage_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)
	manage_lnk_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)
	manage_sock_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)

	relabel_fifo_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)
	relabel_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)
	relabel_lnk_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)
	relabel_sock_files_pattern($2, mozilla_tmpfs_t, mozilla_tmpfs_t)

	mozilla_stream_connect($2)

	optional_policy(`
		mozilla_dbus_chat($2)
	')

	optional_policy(`
		nsplugin_role($1, mozilla_t)
	')

	optional_policy(`
		pulseaudio_role($1, mozilla_t)
	')
')

########################################
## <summary>
##	Read mozilla home directory content
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_read_user_home_files',`
	gen_require(`
		type mozilla_home_t;
	')

	allow $1 mozilla_home_t:dir list_dir_perms;
	allow $1 mozilla_home_t:file read_file_perms;
	allow $1 mozilla_home_t:lnk_file read_lnk_file_perms;
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Write mozilla home directory content
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_write_user_home_files',`
	gen_require(`
		type mozilla_home_t;
	')

	write_files_pattern($1, mozilla_home_t, mozilla_home_t)
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Dontaudit attempts to read/write mozilla home directory content
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`mozilla_dontaudit_rw_user_home_files',`
	gen_require(`
		type mozilla_home_t;
	')

	dontaudit $1 mozilla_home_t:file rw_inherited_file_perms;
')

########################################
## <summary>
##	Manage mozilla home directory content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_manage_user_home_content',`
	gen_require(`
		type mozilla_home_t;
	')

	manage_files_pattern($1, mozilla_home_t, mozilla_home_t)
	manage_lnk_files_pattern($1, mozilla_home_t, mozilla_home_t)
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Dontaudit attempts to write mozilla home directory content
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`mozilla_dontaudit_manage_user_home_files',`
	gen_require(`
		type mozilla_home_t;
	')

	dontaudit $1 mozilla_home_t:dir manage_dir_perms;
	dontaudit $1 mozilla_home_t:file manage_file_perms;
')

########################################
## <summary>
##	Execmod mozilla home directory content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_execmod_user_home_files',`
	gen_require(`
		type mozilla_home_t;
	')

	allow $1 mozilla_home_t:file execmod;
')

########################################
## <summary>
##	Run mozilla in the mozilla domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`mozilla_domtrans',`
	gen_require(`
		type mozilla_t, mozilla_exec_t;
	')

	domtrans_pattern($1, mozilla_exec_t, mozilla_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Send and receive messages from
##	mozilla over dbus.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_dbus_chat',`
	gen_require(`
		type mozilla_t;
		class dbus send_msg;
	')

	allow $1 mozilla_t:dbus send_msg;
	allow mozilla_t $1:dbus send_msg;
')

########################################
## <summary>
##	read/write mozilla per user tcp_socket
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_rw_tcp_sockets',`
	gen_require(`
		type mozilla_t;
	')

	allow $1 mozilla_t:tcp_socket rw_socket_perms;
')

########################################
## <summary>
##	Execute mozilla_exec_t 
##	in the specified domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="target_domain">
##	<summary>
##	The type of the new process.
##	</summary>
## </param>
#
interface(`mozilla_exec_domtrans',`
	gen_require(`
		type mozilla_exec_t;
	')

	allow $2 mozilla_exec_t:file entrypoint;
	domtrans_pattern($1, mozilla_exec_t, $2)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Read Mozilla tmpfs files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_read_tmpfs_files',`
	gen_require(`
		type mozilla_tmpfs_t;
	')

	allow $1 mozilla_tmpfs_t:file read_file_perms;
	fs_search_tmpfs($1)
')

########################################
## <summary>
##	Delete Mozilla tmpfs files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_delete_tmpfs_files', `
	gen_require(`
		type mozilla_tmpfs_t;
	')

	allow $1 mozilla_tmpfs_t:file delete_file_perms;
	fs_search_tmpfs($1)
')

########################################
## <summary>
##	Stream connect to Mozilla.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mozilla_stream_connect',`
	gen_require(`
		type mozilla_t, mozilla_tmp_t;
	')

	stream_connect_pattern($1, mozilla_tmp_t, mozilla_tmp_t, mozilla_t)
	files_search_tmp($1)
')
