## <summary>Thunderbird email client.</summary>

########################################
## <summary>
##	Role access for thunderbird
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`thunderbird_role',`
	gen_require(`
		type thunderbird_t, thunderbird_exec_t;
		type thunderbird_tmp_t;
	')

	role $1 types thunderbird_t;

	domain_auto_trans($2, thunderbird_exec_t, thunderbird_t)

	# This is ugly: fix me
	allow $2 thunderbird_t:fd use;
	allow $2 thunderbird_t:shm { associate getattr };
	allow thunderbird_t $2:fd use;
	allow thunderbird_t $2:process sigchld;

	ps_process_pattern($2, thunderbird_t)
	allow $2 thunderbird_t:process { ptrace signal_perms };

	manage_dirs_pattern($2, thunderbird_tmp_t, thunderbird_tmp_t)
	manage_files_pattern($2, thunderbird_tmp_t, thunderbird_tmp_t)
	manage_sock_files_pattern($2, thunderbird_tmp_t, thunderbird_tmp_t)
	
	relabel_dirs_pattern($2, thunderbird_tmp_t, thunderbird_tmp_t)
	relabel_files_pattern($2, thunderbird_tmp_t, thunderbird_tmp_t)
	relabel_sock_files_pattern($2, thunderbird_tmp_t, thunderbird_tmp_t)

	thunderbird_stream_connect($2)

	optional_policy(`
		thunderbird_dbus_chat($2)
	')
')

########################################
## <summary>
##	Run thunderbird in the user thunderbird domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`thunderbird_domtrans',`
	gen_require(`
		type thunderbird_t, thunderbird_exec_t;
	')

	domtrans_pattern($1, thunderbird_exec_t, thunderbird_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Send DBus messages to and from
##	Thunderbird.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`thunderbird_dbus_chat',`
	gen_require(`
		type thunderbird_t;
		class dbus send_msg;
	')

	allow $1 thunderbird_t:dbus send_msg;
	allow thunderbird_t $1:dbus send_msg;
')

########################################
## <summary>
##	Do not audit attempts to read and
##	write inherited Thunderbird home
##	files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`thunderbird_dontaudit_rw_inherited_home_files',`
	gen_require(`
		type thunderbird_home_t;
	')

	dontaudit $1 thunderbird_home_t:file rw_inherited_file_perms;
')

########################################
## <summary>
##	Write Thunderbird tmp files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`thunderbird_write_tmp_files',`
	gen_require(`
		type thunderbird_tmp_t;
	')

	allow $1 thunderbird_tmp_t:file write_file_perms;
	files_search_tmp($1)
')

########################################
## <summary>
##	Stream connect to Thunderbird.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`thunderbird_stream_connect',`
	gen_require(`
		type thunderbird_t, thunderbird_tmp_t;
	')

	stream_connect_pattern($1, thunderbird_tmp_t, thunderbird_tmp_t, thunderbird_t)
	files_search_tmp($1)
')

########################################
## <summary>
##	Read Thunderbird tmpfs files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`thunderbird_read_tmpfs_files', `
	gen_require(`
		type thunderbird_tmpfs_t;
	')

	allow $1 thunderbird_tmpfs_t:file read_file_perms;
	fs_search_tmpfs($1)
')

########################################
## <summary>
##	Delete Thunderbird tmpfs files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`thunderbird_delete_tmpfs_files', `
	gen_require(`
		type thunderbird_tmpfs_t;
	')

	allow $1 thunderbird_tmpfs_t:file delete_file_perms;
	fs_search_tmpfs($1)
')


########################################
## <summary>
##	Send signull signals to Thunderbird.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`thunderbird_signull',`
	gen_require(`
		type thunderbird_t;
	')

	allow $1 thunderbird_t:process signull;
')
