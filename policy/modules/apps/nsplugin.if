
## <summary>Nsplugin.</summary>

########################################
## <summary>
##	Create, read, write, and delete
##	nsplugin rw files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_manage_rw_files',`
	gen_require(`
		type nsplugin_rw_t;
	')

	allow $1 nsplugin_rw_t:file manage_file_perms;
	allow $1 nsplugin_rw_t:dir rw_dir_perms;
')

########################################
## <summary>
##	Manage nsplugin rw files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_manage_rw',`
	gen_require(`
		type nsplugin_rw_t;
	')

	manage_dirs_pattern($1, nsplugin_rw_t, nsplugin_rw_t)
	manage_files_pattern($1, nsplugin_rw_t, nsplugin_rw_t)
	manage_lnk_files_pattern($1, nsplugin_rw_t, nsplugin_rw_t)
')

#######################################
## <summary>
##	The per role template for the nsplugin module.
## </summary>
## <param name="userdomain_prefix">
##	<summary>
##	The prefix of the user domain (e.g., user
##	is the prefix for user_t).
##	</summary>
## </param>
## <param name="user_domain">
##	<summary>
##	The type of the user domain.
##	</summary>
## </param>
## <param name="user_role">
##	<summary>
##	The role associated with the user domain.
##	</summary>
## </param>
#
interface(`nsplugin_role_notrans',`
	gen_require(`
		type nsplugin_rw_t;
		type nsplugin_home_t, nsplugin_tmp_t;
		type nsplugin_exec_t, nsplugin_tmpfs_t;
		type nsplugin_config_exec_t;
		type nsplugin_t;
		type nsplugin_config_t;
	')

	role $1 types nsplugin_t;
	role $1 types nsplugin_config_t;

	list_dirs_pattern($2, nsplugin_rw_t, nsplugin_rw_t)
	read_files_pattern($2, nsplugin_rw_t, nsplugin_rw_t)
	read_lnk_files_pattern($2, nsplugin_rw_t, nsplugin_rw_t)
	can_exec($2, nsplugin_rw_t)

	manage_dirs_pattern($2, nsplugin_tmp_t, nsplugin_tmp_t)
	manage_files_pattern($2, nsplugin_tmp_t, nsplugin_tmp_t)
	manage_sock_files_pattern($2, nsplugin_tmp_t, nsplugin_tmp_t)

	relabel_dirs_pattern($2, nsplugin_tmp_t, nsplugin_tmp_t)
	relabel_files_pattern($2, nsplugin_tmp_t, nsplugin_tmp_t)
	relabel_sock_files_pattern($2, nsplugin_tmp_t, nsplugin_tmp_t)

	manage_files_pattern($2, nsplugin_tmpfs_t, nsplugin_tmpfs_t)
	relabel_files_pattern($2, nsplugin_tmpfs_t, nsplugin_tmpfs_t)

	# Clean me
	dontaudit nsplugin_t $2:process ptrace;
	allow nsplugin_t $2:sem rw_sem_perms;
	allow nsplugin_t $2:shm rw_shm_perms;
	dontaudit nsplugin_t $2:shm destroy;
	allow $2 nsplugin_t:sem rw_sem_perms;

	allow $2 nsplugin_t:process { ptrace signal_perms };

	nsplugin_stream_connect($2)

	ifdef(`hide_broken_symptoms',`
		dontaudit nsplugin_t $2:socket_class_set rw_inherited_sock_file_perms;
		dontaudit nsplugin_t $2:fifo_file rw_inherited_fifo_file_perms;
		dontaudit nsplugin_config_t $2:socket_class_set rw_inherited_sock_file_perms;
		dontaudit nsplugin_config_t $2:fifo_file rw_inherited_fifo_file_perms;
	')

	optional_policy(`
		nsplugin_dbus_chat($2)
	')

	optional_policy(`
		pulseaudio_role($1, nsplugin_t)
	')
')

#######################################
## <summary>
##	Role access for nsplugin
## </summary>
## <param name="userdomain_prefix">
##	<summary>
##	The prefix of the user domain (e.g., user
##	is the prefix for user_t).
##	</summary>
## </param>
## <param name="user_role">
##	<summary>
##	The role associated with the user domain.
##	</summary>
## </param>
## <param name="user_domain">
##	<summary>
##	The type of the user domain.
##	</summary>
## </param>
#
interface(`nsplugin_role',`
	gen_require(`
		type nsplugin_exec_t, nsplugin_config_exec_t;
		type nsplugin_t, nsplugin_config_t;
	')

	nsplugin_role_notrans($1, $2)

	domtrans_pattern($2, nsplugin_exec_t, nsplugin_t)
	domtrans_pattern($2, nsplugin_config_exec_t, nsplugin_config_t)
')

#######################################
## <summary>
##	Run nsplugin in the nsplugin domain.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`nsplugin_domtrans',`
	gen_require(`
		type nsplugin_exec_t;
		type nsplugin_t;
	')

	domtrans_pattern($1, nsplugin_exec_t, nsplugin_t)
	corecmd_search_bin($1)

	# This doesnt belong here
	allow $1 nsplugin_t:unix_stream_socket connectto;
	allow nsplugin_t $1:process signal;
')

#######################################
## <summary>
##	Run nsplugin config in the nsplugin
##	config domain.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`nsplugin_domtrans_config',`
	gen_require(`
		type nsplugin_config_exec_t;
		type nsplugin_config_t;
	')

	domtrans_pattern($1, nsplugin_config_exec_t, nsplugin_config_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Search nsplugin rw directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_search_rw_dir',`
	gen_require(`
		type nsplugin_rw_t;
	')

	allow $1 nsplugin_rw_t:dir search_dir_perms;
')

########################################
## <summary>
##	Read nsplugin rw files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_read_rw_files',`
	gen_require(`
		type nsplugin_rw_t;
	')

	list_dirs_pattern($1, nsplugin_rw_t, nsplugin_rw_t)
	read_files_pattern($1, nsplugin_rw_t, nsplugin_rw_t)
	read_lnk_files_pattern($1, nsplugin_rw_t, nsplugin_rw_t)
')

########################################
## <summary>
##	Read nsplugin home files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_read_home',`
	gen_require(`
		type nsplugin_home_t;
	')

	list_dirs_pattern($1, nsplugin_home_t, nsplugin_home_t)
	read_files_pattern($1, nsplugin_home_t, nsplugin_home_t)
	read_lnk_files_pattern($1, nsplugin_home_t, nsplugin_home_t)
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Exec nsplugin rw files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_rw_exec',`
	gen_require(`
		type nsplugin_rw_t;
	')

	can_exec($1, nsplugin_rw_t)
')

########################################
## <summary>
##	Create, read, write, and delete
##	nsplugin home files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_manage_home_files',`
	gen_require(`
		type nsplugin_home_t;
	')

	manage_files_pattern($1, nsplugin_home_t, nsplugin_home_t)
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Read and write to
##	nsplugin named pipes.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_rw_pipes',`
	gen_require(`
		type nsplugin_home_t;
	')

	allow $1 nsplugin_home_t:fifo_file rw_fifo_file_perms; 
')

########################################
## <summary>
##	Read and write to nsplugin shared memory.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_rw_shm',`
	gen_require(`
		type nsplugin_t;
	')

	allow $1 nsplugin_t:shm rw_shm_perms;
')

#####################################
## <summary>
##	Read and write access to nsplugin semaphores.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_rw_semaphores',`
	gen_require(`
		type nsplugin_t;
	')

	allow $1 nsplugin_t:sem rw_sem_perms;
')

########################################
## <summary>
##	Execute nsplugin_exec_t 
##	in the specified domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="target_domain">
##	<summary>
##	The type of the new process.
##	</summary>
## </param>
#
interface(`nsplugin_exec_domtrans',`
	gen_require(`
		type nsplugin_exec_t;
	')

	allow $2 nsplugin_exec_t:file entrypoint;
	domtrans_pattern($1, nsplugin_exec_t, $2)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Send and receive messages from
##	nsplugin over dbus.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_dbus_chat',`
	gen_require(`
		type nsplugin_t;
		class dbus send_msg;
	')

	allow $1 nsplugin_t:dbus send_msg;
	allow nsplugin_t $1:dbus send_msg;
')

########################################
## <summary>
##	Stream connect to nsplugin.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_stream_connect',`
	gen_require(`
		type nsplugin_t, nsplugin_tmp_t;
	')

	stream_connect_pattern($1, nsplugin_tmp_t, nsplugin_tmp_t, nsplugin_t)
	files_search_tmp($1)
')

########################################
## <summary>
##	Read nsplugin tmpfs files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_read_tmpfs_files',`
	gen_require(`
		type nsplugin_tmpfs_t;
	')

	allow $1 nsplugin_tmpfs_t:file read_file_perms;
	fs_search_tmpfs($1)
')

########################################
## <summary>
##	Delete nsplugin tmpfs files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_delete_tmpfs_files',`
	gen_require(`
		type nsplugin_tmpfs_t;
	')

	allow $1 nsplugin_tmpfs_t:file delete_file_perms;
	fs_search_tmpfs($1)
')

########################################
## <summary>
##	Read and write inherited nsplugin
##	tmpfs files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nsplugin_rw_inherited_tmpfs_files',`
	gen_require(`
		type nsplugin_tmpfs_t;
	')

	allow $1 nsplugin_tmpfs_t:file rw_inherited_file_perms;
	fs_search_tmpfs($1)
')
