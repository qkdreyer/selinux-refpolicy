## <summary>Java virtual machine.</summary>

########################################
## <summary>
##	Role access for java
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`java_role',`
	gen_require(`
		type java_t, java_exec_t;
	')

	role $1 types java_t;

	domtrans_pattern($2, java_exec_t, java_t)

	# Fix me
	allow java_t $2:process signull;
	allow $2 java_t:process { noatsecure siginh rlimitinh };
	allow java_t $2:unix_stream_socket connectto;
	allow java_t $2:unix_stream_socket rw_inherited_sock_file_perms;
	allow java_t $2:tcp_socket rw_inherited_sock_file_perms;
')

#######################################
## <summary>
##	The role template for the java module.
## </summary>
## <param name="role_prefix">
##	<summary>
##	The prefix of the user domain (e.g., user
##	is the prefix for user_t).
##	</summary>
## </param>
## <param name="user_role">
##	<summary>
##	The role associated with the user domain.
##	</summary>
## </param>
## <param name="user_domain">
##	<summary>
##	The type of the user domain.
##	</summary>
## </param>
#
template(`java_role_template',`
	gen_require(`
		attribute java_domain;
		type java_exec_t;
	')

	type $1_java_t, java_domain;
	domain_type($1_java_t)
	domain_entry_file($1_java_t, java_exec_t)
	role $2 types $1_java_t;

	domain_interactive_fd($1_java_t)

	userdom_unpriv_usertype($1, $1_java_t)
	# Fix me
	userdom_manage_user_tmpfs_files($1_java_t)

	allow $1_java_t self:process { ptrace signal getsched execmem execstack };

	allow $3 $1_java_t:process { ptrace noatsecure signal_perms };

	domtrans_pattern($3, java_exec_t, $1_java_t)

	corecmd_bin_domtrans($1_java_t, $1_t)

	dev_dontaudit_append_rand($1_java_t)

	files_execmod_all_files($1_java_t)

	fs_dontaudit_rw_tmpfs_files($1_java_t)

	ifdef(`hide_broken_symptoms',`
		dontaudit $1_java_t $3:tcp_socket rw_inherited_sock_file_perms;
	')

	# Pulse Audio.
	optional_policy(`
		mozilla_read_tmpfs_files($1_java_t)
		mozilla_delete_tmpfs_files($1_java_t)
		mozilla_signull($1_java_t)
	')

	optional_policy(`
		pulseaudio_read_tmpfs_files($1_java_t)
		pulseaudio_write_tmp_sockets($1_java_t)
	')

	optional_policy(`
		seahorsed_signull($1_java_t)
		seahorsed_read_tmpfs_files($1_java_t)
		seahorsed_delete_tmpfs_files($1_java_t)
	')

	optional_policy(`
		thunderbird_read_tmpfs_files($1_java_t)
		thunderbird_delete_tmpfs_files($1_java_t)
		thunderbird_signull($1_java_t)
	')

	optional_policy(`
		games_signull_tmw($1_java_t)
		tmw_read_tmpfs_files($1_java_t)
		tmw_delete_tmpfs_files($1_java_t)
	')

	optional_policy(`
		totem_signull($1_java_t)
		totem_read_tmpfs_files($1_java_t)
		totem_delete_tmpfs_files($1_java_t)
	')

	optional_policy(`
		vino_server_read_tmpfs_files($1_java_t)
		vino_server_delete_tmpfs_files($1_java_t)
		vino_server_signull($1_java_t)
	')

	optional_policy(`
		wm_signull($1_java_t)
		wm_read_tmpfs_files($1_java_t)
	')

	optional_policy(`
		xserver_role($2, $1_java_t)
	')
')

########################################
## <summary>
##	Run java in javaplugin domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
template(`java_domtrans',`
	gen_require(`
		type java_t, java_exec_t;
	')

	domtrans_pattern($1, java_exec_t, java_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute java in the java domain, and
##	allow the specified role the java domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the java domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`java_run',`
	gen_require(`
		type java_t;
	')

	java_domtrans($1)
	role $2 types java_t;
')

########################################
## <summary>
##	Execute the java program in the unconfined java domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`java_domtrans_unconfined',`
	gen_require(`
		type unconfined_java_t, java_exec_t;
	')

	domtrans_pattern($1, java_exec_t, unconfined_java_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute the java program in the unconfined java domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`java_run_unconfined',`
	gen_require(`
		type unconfined_java_t;
	')

	java_domtrans_unconfined($1)
	role $2 types unconfined_java_t;

	optional_policy(`
		nsplugin_role_notrans($2, unconfined_java_t)
	')
')

########################################
## <summary>
##	Execute the java program in the java domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`java_exec',`
	gen_require(`
		type java_exec_t;
	')

	can_exec($1, java_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Send null signals to java domains.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`java_signull',`
	gen_require(`
		attribute java_domain;
	')

	allow $1 java_domain:process signull;
')
