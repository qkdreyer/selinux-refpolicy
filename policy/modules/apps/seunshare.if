## <summary>Filesystem namespacing/polyinstantiation application.</summary>

########################################
## <summary>
##	Execute a domain transition to run seunshare.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`seunshare_domtrans',`
	gen_require(`
		type seunshare_t, seunshare_exec_t;
	')

	domtrans_pattern($1, seunshare_exec_t, seunshare_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to
##	run seunshare, and allow the specified
##	role the seunshare domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`seunshare_run',`
	gen_require(`
		type seunshare_t;
	')

	seunshare_domtrans($1)
	role $2 types seunshare_t;

	allow $1 seunshare_t:process signal_perms;

	ifdef(`hide_broken_symptoms',`
		dontaudit seunshare_t $1:tcp_socket rw_socket_perms;
		dontaudit seunshare_t $1:udp_socket rw_socket_perms;
		dontaudit seunshare_t $1:unix_stream_socket rw_socket_perms;
	')
')

########################################
## <summary>
##	The role template for the seunshare module.
## </summary>
## <param name="role_prefix">
##	<summary>
##	The prefix of the user role (e.g., user
##	is the prefix for user_r).
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role.
##	</summary>
## </param>
#
interface(`seunshare_role_template',`
	gen_require(`
		attribute seunshare_domain;
		type seunshare_exec_t;
	')

	type $1_seunshare_t, seunshare_domain;
	application_domain($1_seunshare_t, seunshare_exec_t)
	# ubac_constrained($1_seunshare_t)
	role $2 types $1_seunshare_t;

	mls_process_set_level($1_seunshare_t)

	domtrans_pattern($3, seunshare_exec_t, $1_seunshare_t)
	sandbox_transition($1_seunshare_t, $2)

	ps_process_pattern($3, $1_seunshare_t)
	allow $3 $1_seunshare_t:process { ptrace signal_perms };

	allow $1_seunshare_t $3:process transition;
	dontaudit $1_seunshare_t $3:process { noatsecure siginh rlimitinh };

	ifdef(`hide_broken_symptoms', `
		dontaudit $1_seunshare_t $3:socket_class_set rw_inherited_sock_file_perms;
	')
')
