## <summary>Various IRC clients</summary>

########################################
## <summary>
##	Role access for IRC
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`irc_role',`
	gen_require(`
		attribute irc_domain;
		type irc_t, irc_exec_t, irc_home_t, irc_tmp_t;
		type irssi_t, irssi_exec_t;
	')

	########################################
	#
	# Global shared declarations.
	#

	########################################
	#
	# Irc shared declarations.
	#

	role $1 types irc_t;

	########################################
	#
	# Irssi shared declarations.
	#

	role $1 types irssi_t;

	########################################
	#
	# Global shared policy.
	#

	ps_process_pattern($2, irc_domain)
	allow $2 irc_domain:process signal;

	manage_dirs_pattern($2, irc_home_t, irc_home_t)
	manage_files_pattern($2, irc_home_t, irc_home_t)
	manage_lnk_files_pattern($2, irc_home_t, irc_home_t)

	relabel_dirs_pattern($2, irc_home_t, irc_home_t)
	relabel_files_pattern($2, irc_home_t, irc_home_t)
	relabel_lnk_files_pattern($2, irc_home_t, irc_home_t)

	########################################
	#
	# Irc shared policy.
	#

	domtrans_pattern($2, irc_exec_t, irc_t)

	manage_dirs_pattern($2, irc_tmp_t, irc_tmp_t)
	manage_files_pattern($2, irc_tmp_t, irc_tmp_t)
	manage_lnk_files_pattern($2, irc_tmp_t, irc_tmp_t)
	manage_fifo_files_pattern($2, irc_tmp_t, irc_tmp_t)
	manage_sock_files_pattern($2, irc_tmp_t, irc_tmp_t)

	relabel_dirs_pattern($2, irc_tmp_t, irc_tmp_t)
	relabel_files_pattern($2, irc_tmp_t, irc_tmp_t)
	relabel_lnk_files_pattern($2, irc_tmp_t, irc_tmp_t)
	relabel_fifo_files_pattern($2, irc_tmp_t, irc_tmp_t)
	relabel_sock_files_pattern($2, irc_tmp_t, irc_tmp_t)

	########################################
	#
	# Irssi shared policy.
	#

	domtrans_pattern($2, irssi_exec_t, irssi_t)
')
