## <summary>Pulseaudio network sound server.</summary>

########################################
## <summary>
##	Role access for pulseaudio
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`pulseaudio_role',`
	gen_require(`
		type pulseaudio_t, pulseaudio_exec_t, print_spool_t;
		class dbus acquire_svc;
	')

	role $1 types pulseaudio_t;

	domtrans_pattern($2, pulseaudio_exec_t, pulseaudio_t)

	ps_process_pattern($2, pulseaudio_t)

	# Clean this up. pulseaudio_signal($2)
	allow pulseaudio_t $2:process { signal signull };

	ps_process_pattern(pulseaudio_t, $2)
	allow $2 pulseaudio_t:process { ptrace signal_perms };

	# Clean this up. dbus_connect_to($2, pulseaudio_t)
	allow pulseaudio_t $2:dbus acquire_svc;

	pulseaudio_stream_connect($2)

	userdom_manage_home_role($1, pulseaudio_t)
	userdom_manage_tmp_role($1, pulseaudio_t)
	userdom_manage_tmpfs_role($1, pulseaudio_t)

	optional_policy(`
		pulseaudio_dbus_chat($2)
	')
')

########################################
## <summary>
##	Execute a domain transition to run
##	Pulse Audio.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`pulseaudio_domtrans',`
	gen_require(`
		type pulseaudio_t, pulseaudio_exec_t;
	')

	domtrans_pattern($1, pulseaudio_exec_t, pulseaudio_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to run
##	Pulse Audio, and allow the specified
##	role the Pulse Audio domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the Pulse
##	Audio domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`pulseaudio_run',`
	gen_require(`
		type pulseaudio_t;
	')

	pulseaudio_domtrans($1)
	role $2 types pulseaudio_t;
')

########################################
## <summary>
##	Execute Pulse Audio.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed access.
## </summary>
## </param>
#
interface(`pulseaudio_exec',`
	gen_require(`
		type pulseaudio_exec_t;
	')

	can_exec($1, pulseaudio_exec_t)
	corecmd_search_bin($1)
')


########################################
## <summary>
##	Do not audit attempts to execute
##	Pulse Audio.
## </summary>
## <param name="domain">
## <summary>
##	Domain to not audit.
## </summary>
## </param>
#
interface(`pulseaudio_dontaudit_exec',`
	gen_require(`
		type pulseaudio_exec_t;
	')

	dontaudit $1 pulseaudio_exec_t:file exec_file_perms;
')

########################################
## <summary>
##	Send null signals to Pulse Audio.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_signull',`
	gen_require(`
		type pulseaudio_t;
	')

	allow $1 pulseaudio_t:process signull;
')

#####################################
## <summary>
##	Connect to Pulse Audio over a unix
##	stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_stream_connect',`
	gen_require(`
		type pulseaudio_t, pulseaudio_var_run_t;
	')

	
	stream_connect_pattern($1, pulseaudio_var_run_t, pulseaudio_var_run_t, pulseaudio_t)
	files_search_pids($1)
')

########################################
## <summary>
##	Send and receive messages from
##	Pulse Audio over dbus.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_dbus_chat',`
	gen_require(`
		type pulseaudio_t;
		class dbus send_msg;
	')

	allow $1 pulseaudio_t:dbus send_msg;
	allow pulseaudio_t $1:dbus send_msg;
')

########################################
## <summary>
##	Set attributes of Pulse Audio home
##	directories.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_setattr_home_dir',`
	gen_require(`
		type pulseaudio_home_t;
	')

	allow $1 pulseaudio_home_t:dir setattr_dir_perms;
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Read Pulse Audio home files.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_read_home_files',`
	gen_require(`
		type pulseaudio_home_t;
	')

	read_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Read and write Pulse Audio files.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_rw_home_files',`
	gen_require(`
		type pulseaudio_home_t;
	')

	rw_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Manage Pulse Audio home directories.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_manage_home_files',`
	gen_require(`
		type pulseaudio_home_t;
	')

	manage_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	read_lnk_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	userdom_search_user_home_dirs($1)
')
