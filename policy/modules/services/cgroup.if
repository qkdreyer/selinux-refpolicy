## <summary>libcg is a library that abstracts the control group file system in Linux.</summary>
## <desc>
##	<p>
##	libcg aims to provide programmers easily usable APIs to use the control group file system.
##	</p>
## </desc>

########################################
## <summary>
##	Execute a domain transition to run cgexec.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`cgroup_domtrans_cgexec',`
	gen_require(`
		type cgexec_t, cgexec_exec_t;
	')

	domtrans_pattern($1, cgexec_exec_t, cgexec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to run cgclear.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`cgroup_domtrans_cgclear',`
	gen_require(`
		type cgclear_t, cgclear_exec_t;
	')

	domtrans_pattern($1, cgclear_exec_t, cgclear_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to run cgconfig.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`cgroup_domtrans_cgconfigparser',`
	gen_require(`
		type cgconfigparser_t, cgconfigparser_exec_t;
	')

	domtrans_pattern($1, cgconfigparser_exec_t, cgconfigparser_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute cgconfigparser server in the
##	cgconfigparser domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`cgroup_initrc_domtrans_cgconfigparser',`
	gen_require(`
		type cgconfig_initrc_exec_t;
	')

	files_search_etc($1)
	init_labeled_script_domtrans($1, cgconfig_initrc_exec_t)
')

########################################
## <summary>
##	Execute a domain transition to run cgred.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`cgroup_domtrans_cgred',`
	gen_require(`
		type cgred_t, cgred_exec_t;
	')

	domtrans_pattern($1, cgred_exec_t, cgred_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute cgred server in the
##	cgred domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`cgroup_initrc_domtrans_cgred',`
	gen_require(`
		type cgred_initrc_exec_t;
	')

	files_search_etc($1)
	init_labeled_script_domtrans($1, cgred_initrc_exec_t)
')

########################################
## <summary>
##	Delete cgroup directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_delete_cgroup_dirs', `
	gen_require(`
		type cgroup_t;
	')

	delete_dirs_pattern($1, cgroup_t, cgroup_t)
	cgroup_search_cgroup_dirs($1)
')

########################################
## <summary>
##	Execute cgexec without a transition.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_exec_cgexec',`
	gen_require(`
		type cgexec_exec_t;
	')

	can_exec($1, cgexec_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute cgclear without a transition.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_exec_cgclear',`
	gen_require(`
		type cgclear_exec_t;
	')

	can_exec($1, cgclear_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	List cgroup directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_list_cgroup_dirs', `
	gen_require(`
		type cgroup_t;
	')

	allow $1 cgroup_t:dir list_dir_perms;
')

########################################
## <summary>
##	Manage cgroup directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_manage_cgroup_dirs', `
	gen_require(`
		type cgroup_t;
	')

	allow $1 cgroup_t:dir manage_dir_perms;
')

########################################
## <summary>
##	Read and write cgroup directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_rw_cgroup_dirs', `
	gen_require(`
		type cgroup_t;
	')

	allow $1 cgroup_t:dir rw_dir_perms;
')

########################################
## <summary>
##	Search cgroup directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_search_cgroup_dirs', `
	gen_require(`
		type cgroup_t;
	')

	allow $1 cgroup_t:dir search_dir_perms;
')

########################################
## <summary>
##	Read and write cgred sock file in /var/run.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cgroup_stream_connect', `
	gen_require(`
		type cgred_var_run_t, cgred_t;
	')

	stream_connect_pattern($1, cgred_var_run_t, cgred_var_run_t, cgred_t)
	files_search_pids($1)
')

########################################
## <summary>
##	All of the rules required to administrate 
##	an cgroup environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`cgroup_admin',`
	gen_require(`
		type cgred_t, cgconfigparser_t, cgred_var_run_t;
		type cgconfig_etc_t, cgconfig_initrc_exec_t, cgred_initrc_exec_t;
		type cgred_etc_t, cgroup_t, cgclear_t, cgroupfs_t;	
	')

	allow $1 cgclear_t:process { ptrace signal_perms getattr };
	read_files_pattern($1, cgclear_t, cgclear_t)

	allow $1 cgconfigparser_t:process { ptrace signal_perms getattr };
	read_files_pattern($1, cgconfigparser_t, cgconfigparser_t)

	allow $1 cgred_t:process { ptrace signal_perms getattr };
	read_files_pattern($1, cgred_t, cgred_t)

	admin_pattern($1, cgroup_t)
	admin_pattern($1, cgroupfs_t)

	files_search_etc($1)
	admin_pattern($1, cgconfig_etc_t)
	admin_pattern($1, cgred_etc_t)

	files_list_var($1)
	admin_pattern($1, cgred_var_run_t)

	cgroup_domtrans_cgclear($1)
	role_transition $2 cgclear_exec_t system_r;

	cgroup_initrc_domtrans_cgconfigparser($1)
	domain_system_change_exemption($1)
	role_transition $2 cgconfig_initrc_exec_t system_r;
	allow $2 system_r;

	cgroup_initrc_domtrans_cgred($1)
	role_transition $2 cgred_initrc_exec_t system_r;
')

