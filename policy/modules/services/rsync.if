## <summary>Fast incremental file transfer for synchronization.</summary>

########################################
## <summary>
##	Make rsync an entry point for
##	the specified domain.
## </summary>
## <param name="domain">
##	<summary>
##	The domain for which init scripts are an entrypoint.
##	</summary>
## </param>
#
interface(`rsync_entry_type',`
	gen_require(`
		type rsync_exec_t;
	')

	domain_entry_file($1, rsync_exec_t)
')

########################################
## <summary>
##	Execute a rsync in a specified domain.
## </summary>
## <param name="source_domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="target_domain">
##	<summary>
##	Domain to transition to.
##	</summary>
## </param>
#
interface(`rsync_entry_spec_domtrans',`
	gen_require(`
		type rsync_exec_t;
	')

	domain_trans($1, rsync_exec_t, $2)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a rsync in a specified domain.
## </summary>
## <param name="source_domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="target_domain">
##	<summary>
##	Domain to transition to.
##	</summary>
## </param>
#
interface(`rsync_entry_domtrans',`
	gen_require(`
		type rsync_exec_t;
	')

	domain_auto_trans($1, rsync_exec_t, $2)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute rsync.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`rsync_exec',`
	gen_require(`
		type rsync_exec_t;
	')

	can_exec($1, rsync_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Read rsync config files.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed.
## </summary>
## </param>
#
interface(`rsync_read_config',`
	gen_require(`
		type rsync_etc_t;
	')

	read_files_pattern($1, rsync_etc_t, rsync_etc_t)
	files_search_etc($1)
')

########################################
## <summary>
##	Write to rsync config files.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed.
## </summary>
## </param>
#
interface(`rsync_write_config',`
	gen_require(`
		type rsync_etc_t;
	')

	write_files_pattern($1, rsync_etc_t, rsync_etc_t)
	files_search_etc($1)
')
