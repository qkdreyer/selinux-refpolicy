## <summary>ClamAV Virus Scanner.</summary>

########################################
## <summary>
##	Execute a domain transition to run clamd.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`clamav_domtrans',`
	gen_require(`
		type clamd_t, clamd_exec_t;
	')

	domtrans_pattern($1, clamd_exec_t, clamd_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to run clamsmtpd.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`clamav_domtrans_clamsmtpd',`
	gen_require(`
		type clamsmtpd_t, clamsmtpd_exec_t;
	')

	domtrans_pattern($1, clamsmtpd_exec_t, clamsmtpd_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute clamsmtpd server in the clamsmtpd domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`clamav_initrc_domtrans_clamsmtpd',`
	gen_require(`
		type clamsmtpd_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, clamsmtpd_initrc_exec_t)
')

########################################
## <summary>
##	Connect to run clamd.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to connect.
##	</summary>
## </param>
#
interface(`clamav_stream_connect',`
	gen_require(`
		type clamd_t, clamd_var_run_t;
	')

	stream_connect_pattern($1, clamd_var_run_t, clamd_var_run_t, clamd_t)
	files_search_pids($1)
')

########################################
## <summary>
##	Allow the specified domain to append
##	to clamav log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`clamav_append_log',`
	gen_require(`
		type clamav_log_t;
	')

	allow $1 clamav_log_t:dir list_dir_perms;
	append_files_pattern($1, clamav_log_t, clamav_log_t)
	logging_search_logs($1)
')

########################################
## <summary>
##	Read clamav configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`clamav_read_config',`
	gen_require(`
		type clamd_etc_t;
	')

	allow $1 clamd_etc_t:file read_file_perms;
	files_search_etc($1)
')

########################################
## <summary>
##	Read clamsmtpd configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`clamav_read_clamsmtpd_config',`
	gen_require(`
		type clamsmtpd_etc_t;
	')

	allow $1 clamsmtpd_etc_t:file read_file_perms;
	files_search_etc($1)
')

########################################
## <summary>
##	Search clamav libraries directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`clamav_search_lib',`
	gen_require(`
		type clamd_var_lib_t;
	')

	allow $1 clamd_var_lib_t:dir search_dir_perms;
	files_search_var_lib($1)
')

########################################
## <summary>
##	Read clamsmtpd temporary files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`clamav_read_clamsmtpd_tmp_files',`
	gen_require(`
		type clamsmtpd_tmp_t;
	')

	read_files_pattern($1, clamsmtpd_tmp_t, clamsmtpd_tmp_t)
	files_search_tmp($1)
')

########################################
## <summary>
##	Execute a domain transition to run clamscan.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`clamav_domtrans_clamscan',`
	gen_require(`
		type clamscan_t, clamscan_exec_t;
	')

	domtrans_pattern($1, clamscan_exec_t, clamscan_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute clamscan without a transition.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`clamav_exec_clamscan',`
	gen_require(`
		type clamscan_exec_t;
	')

	can_exec($1, clamscan_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	All of the rules required to administrate
##	an clamav environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed to manage the clamav domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`clamav_admin',`
	gen_require(`
		type clamd_t, clamd_etc_t, clamd_tmp_t;
		type clamd_var_log_t, clamd_var_lib_t;
		type clamd_var_run_t, clamscan_t, clamscan_tmp_t;
		type clamd_initrc_exec_t;
		type freshclam_t, freshclam_var_log_t;
		type clamsmtpd_t, clamsmtpd_initrc_exec_t;
		type clamsmtpd_etc_t, clamsmtpd_tmp_t;
	')

	allow $1 clamd_t:process { ptrace signal_perms };
	ps_process_pattern($1, clamd_t)

	allow $1 clamscan_t:process { ptrace signal_perms };
	ps_process_pattern($1, clamscan_t)

	allow $1 clamsmtpd_t:process { ptrace signal_perms };
	ps_process_pattern($1, clamsmtpd_t)

	allow $1 freshclam_t:process { ptrace signal_perms };
	ps_process_pattern($1, freshclam_t)

	init_labeled_script_domtrans($1, clamd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 clamd_initrc_exec_t system_r;
	allow $2 system_r;

	init_labeled_script_domtrans($1, clamsmtpd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 clamsmtpd_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_etc($1)
	admin_pattern($1, clamd_etc_t)

	files_list_var_lib($1)
	admin_pattern($1, clamd_var_lib_t)

	logging_list_logs($1)
	admin_pattern($1, clamd_var_log_t)

	files_list_pids($1)
	admin_pattern($1, clamd_var_run_t)

	files_list_tmp($1)
	admin_pattern($1, clamd_tmp_t)

	admin_pattern($1, clamscan_tmp_t)

	admin_pattern($1, clamsmtpd_etc_t)

	admin_pattern($1, clamsmtpd_tmp_t)

	admin_pattern($1, freshclam_var_log_t)
')
