## <summary>Prelink ELF shared libraries and binaries to speed up startup time.</summary>

########################################
## <summary>
##	Execute a domain transition to
##	run Prelink.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`prelink_domtrans',`
	gen_require(`
		type prelink_t, prelink_exec_t;
	')

	domtrans_pattern($1, prelink_exec_t, prelink_t)
	corecmd_search_bin($1)

	ifdef(`hide_broken_symptoms',`
		dontaudit prelink_t $1:socket_class_set rw_inherited_sock_file_perms;
		dontaudit prelink_t $1:fifo_file setattr_fifo_file_perms;
	')
')

########################################
## <summary>
##	Execute Prelink.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_exec',`
	gen_require(`
		type prelink_exec_t;
	')

	can_exec($1, prelink_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to
##	run Prelink and allow the
##	specified role the Prelink domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to allow the Prelink domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`prelink_run',`
	gen_require(`
		type prelink_t;
	')

	prelink_domtrans($1)
	role $2 types prelink_t;
')

########################################
## <summary>
##	Make specified file types
##	Prelinkable.
## </summary>
## <param name="file_type">
##	<summary>
##	File type to be prelinked.
##	</summary>
## </param>
#
interface(`prelink_object_file',`
	gen_require(`
		attribute prelink_object;
	')

	typeattribute $1 prelink_object;
')

########################################
## <summary>
##	Read Prelink cache files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_read_cache',`
	gen_require(`
		type prelink_cache_t;
	')

	allow $1 prelink_cache_t:file read_file_perms;
	files_search_etc($1)
')

########################################
## <summary>
##	Delete Prelink cache files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_delete_cache',`
	gen_require(`
		type prelink_cache_t;
	')

	delete_files_pattern($1, prelink_cache_t, prelink_cache_t)
	files_search_etc($1)
')

########################################
## <summary>
##	Manage Prelink log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_manage_log',`
	gen_require(`
		type prelink_log_t;
	')

	manage_files_pattern($1, prelink_log_t, prelink_log_t)
	logging_search_logs($1)
')

########################################
## <summary>
##	Manage Prelink lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_manage_lib',`
	gen_require(`
		type prelink_var_lib_t;
	')

	manage_files_pattern($1, prelink_var_lib_t, prelink_var_lib_t)
	files_search_var_lib($1)
')

########################################
## <summary>
##	Relabel from Prelink lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_relabelfrom_lib',`
	gen_require(`
		type prelink_var_lib_t;
	')

	relabelfrom_files_pattern($1, prelink_var_lib_t, prelink_var_lib_t)
	files_search_var_lib($1)
')

########################################
## <summary>
##	Relabel Prelink lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_relabel_lib',`
	gen_require(`
		type prelink_var_lib_t;
	')

	relabel_files_pattern($1, prelink_var_lib_t, prelink_var_lib_t)
	files_search_var_lib($1)
')
