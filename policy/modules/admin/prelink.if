## <summary>Prelink ELF shared libraries and binaries to speed up startup time.</summary>

########################################
## <summary>
##	Execute the prelink program in the prelink domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`prelink_domtrans',`
	gen_require(`
		type prelink_t, prelink_exec_t;
	')

	domtrans_pattern($1, prelink_exec_t, prelink_t)
	corecmd_search_bin($1)

	ifdef(`hide_broken_symptoms',`
		dontaudit prelink_t $1:socket_class_set { read write };
		dontaudit prelink_t $1:fifo_file setattr;
	')
')

########################################
## <summary>
##	Execute the prelink program in the current domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_exec',`
	gen_require(`
		type prelink_exec_t;
	')

	can_exec($1, prelink_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute the prelink program in the prelink domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to allow the prelink domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`prelink_run',`
	gen_require(`
		type prelink_t;
	')

	prelink_domtrans($1)
	role $2 types prelink_t;
')

########################################
## <summary>
##	Make the specified file type prelinkable.
## </summary>
## <param name="file_type">
##	<summary>
##	File type to be prelinked.
##	</summary>
## </param>
#
interface(`prelink_object_file',`
	gen_require(`
		attribute prelink_object;
	')

	typeattribute $1 prelink_object;
')

########################################
## <summary>
##	Read the prelink cache.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_read_cache',`
	gen_require(`
		type prelink_cache_t;
	')

	allow $1 prelink_cache_t:file read_file_perms;
	files_search_etc($1)
')

########################################
## <summary>
##	Delete the prelink cache.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_delete_cache',`
	gen_require(`
		type prelink_cache_t;
	')

	delete_files_pattern($1, prelink_cache_t, prelink_cache_t)
	files_search_etc($1)
')

########################################
## <summary>
##	Create, read, write, and delete
##	prelink log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_manage_log',`
	gen_require(`
		type prelink_log_t;
	')

	manage_files_pattern($1, prelink_log_t, prelink_log_t)
	logging_search_logs($1)
')

########################################
## <summary>
##	Create, read, write, and delete
##	prelink var_lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_manage_lib',`
	gen_require(`
		type prelink_var_lib_t;
	')

	manage_files_pattern($1, prelink_var_lib_t, prelink_var_lib_t)
	files_search_var_lib($1)
')

########################################
## <summary>
##	Relabel from prelink var lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_relabelfrom_lib',`
	gen_require(`
		type prelink_var_lib_t;
	')

	relabelfrom_files_pattern($1, prelink_var_lib_t, prelink_var_lib_t)
	files_search_var_lib($1)
')

########################################
## <summary>
##	Relabel prelink var lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prelink_relabel_lib',`
	gen_require(`
		type prelink_var_lib_t;
	')

	relabel_files_pattern($1, prelink_var_lib_t, prelink_var_lib_t)
	files_search_var_lib($1)
')
