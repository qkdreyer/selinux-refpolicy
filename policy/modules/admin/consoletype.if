## <summary>Consoletype print type of the console connected to standard input.</summary>

########################################
## <summary>
##	Execute a domain transition to
##	run Consoletype.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`consoletype_domtrans',`
	gen_require(`
		type consoletype_t, consoletype_exec_t;
	')

	domtrans_pattern($1, consoletype_exec_t, consoletype_t)
	corecmd_search_bin($1)

	ifdef(`hide_broken_symptoms',`
		dontaudit consoletype_t $1:socket_class_set rw_inherited_sock_file_perms;
	')
')

########################################
## <summary>
##	Execute a domain transition to
##	run Consoletype and allow the
##	specified role the Consoletype domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the Consoletype
##	domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`consoletype_run',`
	gen_require(`
		type consoletype_t;
	')

	consoletype_domtrans($1)
	role $2 types consoletype_t;
')

########################################
## <summary>
##	Execute Consoletype.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`consoletype_exec',`
	gen_require(`
		type consoletype_exec_t;
	')

	can_exec($1, consoletype_exec_t)
	corecmd_search_bin($1)
')
