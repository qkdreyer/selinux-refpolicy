## <summary>consoletype print type of the console connected to standard input.</summary>
## <desc>
##	<p>
##	consoletype prints the type of console connected to standard input. It prints
##	vt if console is a virtual terminal (/dev/tty* or /dev/console device if not
##	on a serial console), serial if standard input is a serial console (/dev/console
##	or /dev/ttyS*) and pty if standard input is a pseudo terminal.  
##	</p>
## </desc>

########################################
## <summary>
##	Execute consoletype in the consoletype domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`consoletype_domtrans',`
	gen_require(`
		type consoletype_t, consoletype_exec_t;
	')

	domtrans_pattern($1, consoletype_exec_t, consoletype_t)
	corecmd_search_bin($1)

	ifdef(`hide_broken_symptoms',`
		dontaudit consoletype_t $1:socket_class_set { read write };
	')
')

########################################
## <summary>
##	Execute consoletype in the consoletype domain, and
##	allow the specified role the consoletype domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the consoletype domain.
##	</summary>
## </param>
#
interface(`consoletype_run',`
	gen_require(`
		type consoletype_t;
	')

	consoletype_domtrans($1)
	role $2 types consoletype_t;
')

########################################
## <summary>
##	Execute consoletype in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`consoletype_exec',`
	gen_require(`
		type consoletype_exec_t;
	')

	can_exec($1, consoletype_exec_t)
	corecmd_search_bin($1)
')
