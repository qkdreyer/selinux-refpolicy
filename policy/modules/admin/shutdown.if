## <summary>Shutdown bring the system down.</summary>

########################################
## <summary>
##	Execute a domain transition to
##	run Shutdown.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`shutdown_domtrans',`
	gen_require(`
		type shutdown_t, shutdown_exec_t;
	')

	domtrans_pattern($1, shutdown_exec_t, shutdown_t)
	corecmd_search_bin($1)

	ifdef(`hide_broken_symptoms',`
		dontaudit shutdown_t $1:socket_class_set rw_inherited_sock_file_perms;
		dontaudit shutdown_t $1:fifo_file rw_inherited_fifo_file_perms;
	')
')


########################################
## <summary>
##	Execute a domain transition to
##	run Shutdown and allow the
##	specified role the Shutdown domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the Shutdown
##	domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`shutdown_run',`
	gen_require(`
		type shutdown_t;
	')

	shutdown_domtrans($1)
	role $2 types shutdown_t;
')

########################################
## <summary>
##	Role access for Shutdown.
## </summary>
## <param name="user_role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <param name="user_domain">
##	<summary>
##	User domain for the role.
##	</summary>
## </param>
## <rolecap/>
#
interface(`shutdown_role',`
	gen_require(`
		type shutdown_t;
	')

	shutdown_domtrans($2)
	role $1 types shutdown_t;

	ps_process_pattern($2, shutdown_t)
	allow $2 shutdown_t:process signal;
')

########################################
## <summary>
##	Recieve child signals from Shutdown.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`shutdown_send_sigchld',`
	gen_require(`
		type shutdown_t;
	')

	allow shutdown_t $1:process signal;
')

########################################
## <summary>
##	Send and receive messages from
##	Shutdown over dbus.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`shutdown_dbus_chat',`
	gen_require(`
		type shutdown_t;
		class dbus send_msg;
	')

	allow $1 shutdown_t:dbus send_msg;
	allow shutdown_t $1:dbus send_msg;
')

########################################
## <summary>
##	Get attributes of Shutdown
##	executable files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`shutdown_getattr_exec_files',`
	gen_require(`
		type shutdown_exec_t;
	')

	allow $1 shutdown_exec_t:file getattr_file_perms;
	corecmd_search_bin($1)
')
