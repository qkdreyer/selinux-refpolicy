## <summary>Common Unix Disk Quota Utilities.</summary>

########################################
## <summary>
##	Execute a domain transition to
##	run Quota utilities.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`quota_domtrans',`
	gen_require(`
		type quota_t, quota_exec_t;
	')

	domtrans_pattern($1, quota_exec_t, quota_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to
##	run Quota utilities and allow the
##	specified role the Quota utilities
##	domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the Quota
##	utilities domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`quota_run',`
	gen_require(`
		type quota_t;
	')

	quota_domtrans($1)
	role $2 types quota_t;
')

########################################
## <summary>
##	Do not audit attempts to get
##	attributes of filesystem Quota data
##	files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`quota_dontaudit_getattr_db',`
	gen_require(`
		type quota_db_t;
	')

	dontaudit $1 quota_db_t:file getattr_file_perms;
')

########################################
## <summary>
##	Manage Quota utilities flag files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`quota_manage_flags',`
	gen_require(`
		type quota_flag_t;
	')

	manage_files_pattern($1, quota_flag_t, quota_flag_t)
	files_search_var_lib($1)
')
