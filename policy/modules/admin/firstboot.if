## <summary>Firstboot Initial system configuration utility.</summary>

########################################
## <summary>
##	Execute a domain transition to
##	run Firstboot.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`firstboot_domtrans',`
	gen_require(`
		type firstboot_t, firstboot_exec_t;
	')

	domtrans_pattern($1, firstboot_exec_t, firstboot_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute a domain transition to
##	run Firstboot and allow the
##	specified role the Firstboot domain
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the Firstboot
##	domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`firstboot_run',`
	gen_require(`
		type firstboot_t;
	')

	firstboot_domtrans($1)
	role $2 types firstboot_t;
')

########################################
## <summary>
##	Inherit and use Firstboot file
##	descriptors.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`firstboot_use_fds',`
	gen_require(`
		type firstboot_t;
	')

	allow $1 firstboot_t:fd use;
')

########################################
## <summary>
##	Do not audit attempts to inherit
##	Firstboot file descriptors.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`firstboot_dontaudit_use_fds',`
	gen_require(`
		type firstboot_t;
	')

	dontaudit $1 firstboot_t:fd use;
')

########################################
## <summary>
##	Write Firstboot pipes.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`firstboot_write_pipes',`
	gen_require(`
		type firstboot_t;
	')

	allow $1 firstboot_t:fifo_file write_fifo_file_perms;
')

########################################
## <summary>
##	Read and Write Firstboot pipes.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`firstboot_rw_pipes',`
	gen_require(`
		type firstboot_t;
	')

	allow $1 firstboot_t:fifo_file rw_fifo_file_perms;
')

########################################
## <summary>
## 	Do not audit attemps to read and
##	write Firstboot Pipes.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`firstboot_dontaudit_rw_pipes',`
	gen_require(`
		type firstboot_t;
	')

	dontaudit $1 firstboot_t:fifo_file rw_inherited_fifo_file_perms;
')

########################################
## <summary>
## 	Do not audit attemps to read and
##	write Firstboot unix domain stream
##	sockets.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`firstboot_dontaudit_rw_stream_sockets',`
	gen_require(`
		type firstboot_t;
	')

	dontaudit $1 firstboot_t:unix_stream_socket rw_inherited_sock_file_perms;
')
